\chapter{Spatial Kappa Examples}
\label{chap:resources}

\section{Spatial Kappa patterns}
\label{sec:spatialPatterns}

The following are generic shapes, with their equivalent Spatial Kappa representations. These are intended to be copied during model development. Explicitly specified models are shown first to demonstrate the syntax, then more concise versions are shown where possible.


\subsection{1 dimensional patterns}

\subsubsection{Linear array}

\begin{kappasource}
%compartment: array [n] # Replace n with length of array
%channel: intra-array (:array [x] -> :array [x +1]) + (:array [x] -> :array [x -1])
\end{kappasource}

\subsubsection{Circle}

\begin{kappasource}
%compartment: circle [n] # Replace n with number of cells in circle
%channel: intra-circle {\textbackslash}
    (:circle [x] -> :circle [x +1]) + (:circle [x] -> :circle [x -1]) + {\textbackslash}
    (:circle [n -1] -> :circle [0]) + (:circle [0] -> :circle [n -1]) # Replace n-1 as above
\end{kappasource}


\subsection{2 dimensional surfaces}

\subsubsection{Rectangular mesh}

There are 2 variants here, 4-way linked and 8-way linked.

\begin{kappasource}
%compartment: mesh [n][m] # Replace n and m with dimensions of mesh

# 4-way diffusion
%channel: intra-mesh {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y]) + (:mesh [x][y] -> :mesh [x -1][y]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x][y +1]) + (:mesh [x][y] -> :mesh [x][y -1])

# or 8-way diffusion
%channel: intra-mesh {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y]) + (:mesh [x][y] -> :mesh [x -1][y]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x][y +1]) + (:mesh [x][y] -> :mesh [x][y -1]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y +1]) + (:mesh [x][y] -> :mesh [x -1][y -1]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y -1]) + (:mesh [x][y] -> :mesh [x -1][y +1])
\end{kappasource}

These can also be specified using channel types as follows

\begin{kappasource}
%compartment: mesh [n][m] # Replace n and m with dimensions of mesh

# 4-way diffusion
%channel: intra-mesh EdgeNeighbour :mesh -> :mesh

# or 8-way diffusion
%channel: intra-mesh Neighbour :mesh -> :mesh
\end{kappasource}

\subsubsection{Hexagonal mesh}

Again, 2 variants depending on what overall shape is required. The first form has a simpler representation of intra-compartment links, but the overall structure is rhomboid, whereas the second produces an overall rectangular shape at the expense of more complicated link statements.

The second variant demonstrates handling of alternate odd-even linkage depending on the column of the structure.

\begin{kappasource}
%compartment: mesh [n][m] # Replace n and m with dimensions of mesh

# Variant 1 - rhomboid mesh
%channel: intra-mesh {\textbackslash}
    (:mesh [x][y] -> :mesh [x][y +1]) + (:mesh [x][y] -> :mesh [x][y -1]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y]) + (:mesh [x][y] -> :mesh [x -1][y]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y +1]) + (:mesh [x][y] -> :mesh [x -1][y -1])

# Variant 2 - rectangular mesh
%channel: intra-mesh {\textbackslash}
    (:mesh [x][y] -> :mesh [x][y +1]) + (:mesh [x][y] -> :mesh [x][y -1]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y]) + (:mesh [x][y] -> :mesh [x -1][y]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][(y +1)-(2*(x%2))]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x -1][(y -1)+(2*((x -1)%2))])

# The above statement alternates [x +1][y +1] and [x +1][y -1] as x increases
\end{kappasource}

Variant 2 can also be specified using channel types as follows

\begin{kappasource}
%compartment: mesh [n][m] # Replace n and m with dimensions of mesh

# Variant 2 - rectangular mesh
%channel: intra-mesh Hexagonal :mesh -> :mesh
\end{kappasource}


\subsubsection{Cylinder and torus}

By connecting together the top and bottom edges of a mesh as described above, we get a cylinder. By also connecting together the left and right edges we get a torus.

\begin{kappasource}
%compartment: mesh [n][m] # Replace n and m with dimensions of mesh

# 4-way diffusion mesh
%channel: intra-mesh {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y]) + (:mesh [x][y] -> :mesh [x -1][y]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x][y +1]) + (:mesh [x][y] -> :mesh [x][y -1])
%channel: intra-mesh mesh [x][y] <-> mesh [x+1][y] 
%channel: intra-mesh mesh [x][y] <-> mesh [x][y+1] 

# cylinder
%channel: intra-mesh {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y]) + (:mesh [x][y] -> :mesh [x -1][y]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x][y +1]) + (:mesh [x][y] -> :mesh [x][y -1]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x][m -1]) + (:mesh [x][y] -> :mesh [x +1][0]) 
# Replace m-1 as above 

# torus
%channel: intra-mesh {\textbackslash}
    (:mesh [x][y] -> :mesh [x +1][y]) + (:mesh [x][y] -> :mesh [x -1][y]) + {\textbackslash}
    (:mesh [x][y] -> :mesh [x][y +1]) + (:mesh [x][y] -> :mesh [x][y -1]) + {\textbackslash}
    (:mesh [x][m -1] -> :mesh [x][0]) + (:mesh [x][0] -> :mesh [x][m -1]) + {\textbackslash}
    (:mesh [n -1][y] -> :mesh [0][y]) + (:mesh [0][y] -> :mesh [n -1][y])
# Replace n-1 as above 
\end{kappasource}

The predefined compartment \verb|OpenCylinder| allows creation of a cylinder with closed ends

\begin{kappasource}
\%compartment: openCylinder  OpenCylinder  [10][8] [2]    # [diameter][length] [thickness]
\end{kappasource}

\newpage
\section{Sample Spatial Kappa models}

The following are a couple of simple spatial kappa models to demonstrate the use of the language features.



\subsection{2d diffusion model}
\label{sec:2dDiffusion}

This model shows simple diffusion from a point for three distinct species.


\begin{kappasource}
%agent: A()
%agent: B()
%agent: C()

%compartment: cytosol [30][30]

# 6-way diffusion
%channel: hexagonal {\textbackslash}
    (:cytosol [x][y] -> :cytosol [x][y +1]) + (:cytosol [x][y] -> :cytosol [x][y -1]) + {\textbackslash}
    (:cytosol [x][y] -> :cytosol [x +1][y]) + (:cytosol [x][y] -> :cytosol [x -1][y]) + {\textbackslash}
    (:cytosol [x][y] -> :cytosol [x +1][(y +1)-(2*(x%2))]) + {\textbackslash}
    (:cytosol [x][y] -> :cytosol [x -1][(y -1)+(2*((x -1)%2))])

'diffusion' ->:hexagonal @ 0.4

%init: 10000 :cytosol[10][10] A()
%init: 10000 :cytosol[10][14] B()
%init: 10000 :cytosol[14][14] C()

%obs: 'Red' A()
%obs: 'Green' B()
%obs: 'Blue' C()
\end{kappasource}

A more concise version would be

\begin{kappasource}
%agent: A()
%agent: B()
%agent: C()

%compartment: cytosol [30][30]

# 6-way diffusion
%channel: hexagonal Hexagonal :cytosol -> :cytosol

'diffusion' :cytosol ->:hexagonal :cytosol @ 0.4

%init: 10000 :cytosol[10][10] A()
%init: 10000 :cytosol[10][14] B()
%init: 10000 :cytosol[14][14] C()

%obs: 'Red' A()
%obs: 'Green' B()
%obs: 'Blue' C()
\end{kappasource}


\subsection{Bi-trivalent binding model}
\label{sec:bitrivalent}


A variant of bi-trivalent binding test model \citep{yang2008kinetic}. This spatial model allows unbound species to diffuse through the compartment, but bound species remain within a cell of the compartment.


\begin{kappasource}
%agent: A(a,b,bindings~0~1~2)
%agent: B(a,b,c)

%compartment: cytosol [20][20] 

# 6-way diffusion
%channel: hexagonal {\textbackslash}
    (:cytosol [x][y] -> :cytosol [x][y +1]) + (:cytosol [x][y] -> :cytosol [x][y -1]) + {\textbackslash}
    (:cytosol [x][y] -> :cytosol [x +1][y]) + (:cytosol [x][y] -> :cytosol [x -1][y]) + {\textbackslash}
    (:cytosol [x][y] -> :cytosol [x +1][(y +1)-(2*(x%2))]) + {\textbackslash}
    (:cytosol [x][y] -> :cytosol [x -1][(y -1)+(2*((x -1)%2))])

'diffusion-A' A(bindings~0) ->:hexagonal A(bindings~0) @ 0.1
'diffusion-B' B(a,b,c) ->:hexagonal B(a,b,c) @ 1

A(a,b,bindings~0), B(a) -> A(a!1,b,bindings~1),B(a!1) @ 1
A(a,b,bindings~0), B(b) -> A(a!1,b,bindings~1),B(b!1) @ 1
A(a,b,bindings~0), B(c) -> A(a!1,b,bindings~1),B(c!1) @ 1
A(a,b,bindings~0), B(a) -> A(a,b!1,bindings~1),B(a!1) @ 1
A(a,b,bindings~0), B(b) -> A(a,b!1,bindings~1),B(b!1) @ 1
A(a,b,bindings~0), B(c) -> A(a,b!1,bindings~1),B(c!1) @ 1
A(a,b!_,bindings~1), B(a) -> A(a!1,b!_,bindings~2),B(a!1) @ 1
A(a,b!_,bindings~1), B(b) -> A(a!1,b!_,bindings~2),B(b!1) @ 1
A(a,b!_,bindings~1), B(c) -> A(a!1,b!_,bindings~2),B(c!1) @ 1
A(a!_,b,bindings~1), B(a) -> A(a!_,b!1,bindings~2),B(a!1) @ 1
A(a!_,b,bindings~1), B(b) -> A(a!_,b!1,bindings~2),B(b!1) @ 1
A(a!_,b,bindings~1), B(c) -> A(a!_,b!1,bindings~2),B(c!1) @ 1

A(a!1,b,bindings~1),B(a!1) -> A(a,b,bindings~0), B(a) @ 0.01
A(a!1,b,bindings~1),B(b!1) -> A(a,b,bindings~0), B(b) @ 0.01
A(a!1,b,bindings~1),B(c!1) -> A(a,b,bindings~0), B(c) @ 0.01
A(a,b!1,bindings~1),B(a!1) -> A(a,b,bindings~0), B(a) @ 0.01
A(a,b!1,bindings~1),B(b!1) -> A(a,b,bindings~0), B(b) @ 0.01
A(a,b!1,bindings~1),B(c!1) -> A(a,b,bindings~0), B(c) @ 0.01
A(a!1,b!_,bindings~2),B(a!1) -> A(a,b!_,bindings~1), B(a) @ 0.01
A(a!1,b!_,bindings~2),B(b!1) -> A(a,b!_,bindings~1), B(b) @ 0.01
A(a!1,b!_,bindings~2),B(c!1) -> A(a,b!_,bindings~1), B(c) @ 0.01
A(a!_,b!1,bindings~2),B(a!1) -> A(a!_,b,bindings~1), B(a) @ 0.01
A(a!_,b!1,bindings~2),B(b!1) -> A(a!_,b,bindings~1), B(b) @ 0.01
A(a!_,b!1,bindings~2),B(c!1) -> A(a!_,b,bindings~1), B(c) @ 0.01

%init: 600 A(a,b,bindings~0) 
%init: 400 B(a,b,c) 

%obs: 'Red' A(bindings~2) 
%obs: 'Green' A(bindings~1) 
%obs: 'Blue' A(bindings~0) 
\end{kappasource}

