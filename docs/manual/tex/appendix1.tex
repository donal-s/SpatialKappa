\chapter{Spatial Kappa Grammar}
\label{chap:spatialGrammar}



The following is a cut down version of the Antlr grammar used in the Kappa simulator. The syntax has been trimmed for readability, as the original Antlr grammar has artificial constructs for dealing with left recursion, etc. It is read basically as BNF notation with assignments (\verb|variable=bnfConstruct|). The existing basic Kappa grammar is shown in \verb|black|, with the spatial constructs shown as \SaveVerb{Verb}|blue| \newbnf{\UseVerb{Verb}}. 


\begin{bnfsource}
prog :
  (line)*

line :
  agentDecl NEWLINE!
  | \newbnf{compartmentDecl NEWLINE!}
  | \newbnf{channelDecl NEWLINE!}
  | ruleDecl NEWLINE!
  | initDecl NEWLINE!
  | plotDecl NEWLINE!
  | obsDecl NEWLINE!
  | varDecl NEWLINE!
  | modDecl NEWLINE!
  | COMMENT!
  | NEWLINE!

ruleDecl :
  label? transition rate 

transition :
  \newbnf{(source=location)? CHANNEL_TRANSITION channelName=id (target=location)?}
  | \newbnf{(a=agentGroup)? CHANNEL_TRANSITION channelName=id (b=agentGroup)?}
  | (a=agentGroup)? FORWARD_TRANSITION (b=agentGroup)?
  
agentGroup :
  '(' agentGroup ')'
  | \newbnf{location?} agent (',' agent)*

agent :
  id \newbnf{(location)?} '(' (agentInterface (',' agentInterface)*)? ')'

agentInterface :
  id state? link?

state :
  '~' stateId

link :
  '!' INT \newbnf{(':' channelName=id)?}
  | '!' '_' \newbnf{(':' channelName=id)?}
  | '?'

rate :
  '@' varAlgebraExpr

initDecl :
  '\%init:' (INT | label) agentGroup

agentDecl :
  '\%agent:' agentName=id '(' (agentDeclInterface (',' agentDeclInterface)*)? ')'

agentDeclInterface :
  id state*

\newbnf{compartmentDecl :}
  \newbnf{'\%compartment:' name=id (type=id)? ('[' INT ']')*}

\newbnf{channelDecl :}
  \newbnf{'\%channel:' linkName=id channel}
  \newbnf{| '\%channel:' linkName=id '(' channel ')' ('+' '(' channel ')')*}

\newbnf{channel :}
  \newbnf{(type=id)? source=locations FORWARD_TRANSITION target=locations}

\newbnf{locations :}
  \newbnf{location (',' location)*}

\newbnf{location :}
  \newbnf{':' 'fixed'}
  \newbnf{| ':' sourceCompartment=id compartmentIndexExpr*}

\newbnf{compartmentIndexExpr :}
  \newbnf{'[' '?' ']'}
  | \newbnf{'[' cellIndexExpr ']'}

plotDecl :
  '\%plot:' label

obsDecl :
  '\%obs:' label varAlgebraExpr
  | '\%obs:' \newbnf{voxel?} label? agentGroup

varDecl :
  '\%var:' label varAlgebraExpr
  | '\%var:' \newbnf{voxel?} label agentGroup

varAlgebraExpr :
  a=varAlgebraMultExpr (op=operator_add b=varAlgebraMultExpr )*
  
varAlgebraMultExpr :
  a=varAlgebraExpExpr (op=operator_mult b=varAlgebraExpExpr )*
  
varAlgebraExpExpr :
  a=varAlgebraAtom ('^' b=varAlgebraExpExpr )*
  
varAlgebraAtom :
  '(' varAlgebraExpr ')'
  | number
  | label
  | '[' 'inf' ']'
  | '[' 'pi' ']'
  | '[' 'Tsim' ']'
  | '[' 'Tmax' ']'
  | '[' 'Emax' ']'
  | '[' 'T' ']'
  | '[' 'E' ']'
  | operator_unary varAlgebraAtom
  | \newbnf{'|' agentGroup '|'}
  
modDecl :
  '\%mod:' 'repeat' perturbationExpression 'until' booleanExpression
  | '\%mod:' perturbationExpression

perturbationExpression
  '(' perturbationExpression ')'
  | booleanExpression 'do' effects

booleanExpression :
  a=booleanAtom (op=booleanOperator b=booleanAtom )*
  
booleanOperator :
  '&&' | '||'

relationalOperator :
  '<' | '>' | '=' | '<>'

booleanAtom :
  '(' booleanExpression ')'
  | '[' 'true' ']'
  | '[' 'false' ']'
  | '[' 'not' ']' booleanAtom
  | a=varAlgebraExpr relationalOperator b=varAlgebraExpr

effects
  '(' effects ')'
  | effect (';' effect)*

effect :
  '\$SNAPSHOT'
  | '\$STOP'
  | '\$ADD' varAlgebraExpr agentGroup
  | '\$DEL' varAlgebraExpr agentGroup
  | '\$UPDATE' label varAlgebraExpr
  
\newbnf{cellIndexExpr :}
  \newbnf{a=cellIndexAtom operator_cell_index b=cellIndexAtom}
  \newbnf{| a=cellIndexAtom}
  
\newbnf{cellIndexAtom :}
  \newbnf{'(' cellIndexExpr ')'}
  \newbnf{| INT}
  \newbnf{| id}
  
id :
  ( 'a'..'z' | 'A'..'Z' ) ( ALPHANUMERIC | '_' | '-' | '+' )*

stateId :
  ALPHANUMERIC

label :
  LABEL

number :
  ( INT | FLOAT )
  
\newbnf{operator_cell_index :}
  \newbnf{( '+' | '*' | '-' | '/' | '\%' | '^' )}

operator_unary :
  '[' 'log' ']'
  | '[' 'sin' ']'
  | '[' 'cos' ']'
  | '[' 'tan' ']'
  | '[' 'sqrt' ']'
  | '[' 'exp' ']'
  | '[' 'int' ']'

operator_mult :
  '*' | '/' | '[' 'mod' ']'

operator_add :
  '+' | '-'

\newbnf{CHANNEL_TRANSITION :}
  \newbnf{'->:'}

FORWARD_TRANSITION :
  '->'

INT :
  NUMERIC

FLOAT :
  NUMERIC '.' NUMERIC EXPONENT?
  | '.' NUMERIC EXPONENT?
  | NUMERIC EXPONENT

ALPHANUMERIC :
  ( NUMERIC | 'a'..'z' | 'A'..'Z' )+

NUMERIC :
  ('0'..'9')+
  
EXPONENT :
  ('e'|'E') ('+'|'-')? NUMERIC

LABEL :
  '{\textbackslash}'' .* '{\textbackslash}''

COMMENT :
  '#' ~( '{\textbackslash}n' | '{\textbackslash}r' )*

NEWLINE :
  '{\textbackslash}r'? '{\textbackslash}n' | '{\textbackslash}r'

WS :
  ( ' ' | '{\textbackslash}t' | '{\textbackslash}{\textbackslash}' NEWLINE )+
\end{bnfsource}
